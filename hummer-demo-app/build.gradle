apply plugin: 'com.android.application'

def getLocalIPv4() {
    def p = "ifconfig en0".execute() |
            "grep inet".execute() |
            "grep -v inet6".execute() |
            ["cut", "-d", " ", "-f2"].execute()
    p.waitFor()
    return p.text.trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.hummer.demo"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "MOCK_HOST", "\"${getLocalIPv4()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

//repositories{
//    flatDir{
//        dirs 'libs'
//    }
//}

//编译前执行启动JS文件服务器脚本，用于下载js文件
task startRemoteSrv(type: Exec) {
    workingDir "$projectDir"
    commandLine 'sh', '../runScript.sh'
}
//assemble.dependsOn(startRemoteSrv)
preBuild.dependsOn(startRemoteSrv)

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.facebook.soloader:soloader:0.6.0+'

    implementation project(':hummer-sdk')
    implementation project(':hummer-component')
    implementation project(':hummer-annotation')
    implementation project(':hummer-core')

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'

    debugImplementation "com.github.brianPlummer:tinydancer:0.1.2"
    releaseImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"
    testImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"
}
